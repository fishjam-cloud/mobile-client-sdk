import groovy.json.JsonSlurper

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "org.jetbrains.dokka"
    id "com.google.protobuf" version "0.9.4"
    id 'maven-publish'
    id "kotlin-android"
}

def inputFile = new File("$projectDir/../../../package.json")
def packageJson = new JsonSlurper().parseText(inputFile.text)
def packageVersion = packageJson["version"]

ext {
    kotlin_version = '1.8.0'
    java_version = JavaVersion.VERSION_17
}

android {
    namespace 'com.fishjamcloud.client'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        configureEach {
            buildConfigField "String", "PACKAGE_VERSION", "\"${packageVersion}\""
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility java_version
        targetCompatibility java_version
    }
    kotlinOptions {
        jvmTarget = java_version
    }
}

ext {
    customFooterMessage = "Copyright 2023, Software Mansion."
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.fishjamcloud.client'
                artifactId = 'fishjam-android-client'
                version = '0.0.1'
            }
        }
    }
    tasks.named("dokkaHtml") {
        pluginsMapConfiguration.set([
                "org.jetbrains.dokka.base.DokkaBase": """{
                "footerMessage": "$customFooterMessage"
            }"""
        ])
    }
}

tasks.named("dokkaHtml") {
    moduleName.set("FishjamCloudClient")
    dokkaSourceSets {
        named("main") {
            includes.from("module.md")
        }
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.google.protobuf:protobuf-kotlin:4.28.3'
    api 'com.google.protobuf:protobuf-java:4.28.3'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0"
    implementation "com.github.ajalt:timberkt:1.5.1"
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'androidx.core:core:1.8.20'

    api 'io.github.webrtc-sdk:android:125.6422.06'
    compileOnly 'androidx.camera:camera-core:1.5.0-alpha03'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.13.2"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0"
    testImplementation "io.mockk:mockk:1.13.2"
    testImplementation 'org.khronos:opengl-api:gl1.1-android-2.1_r1'

}
